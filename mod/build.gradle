plugins {
	id 'maven-publish'
	id("org.quiltmc.loom") version "1.7.4"
}

base {
	archivesName = project.archives_base_name
}

version = "$project.version+${libs.versions.minecraft.get()}"
group = project.maven_group

repositories {
	mavenCentral()

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
}

loom {
	mods {
		"jline4mcdsrv" {
			sourceSet("main")
		}
	}
}

dependencies {
	minecraft libs.minecraft
	modImplementation libs.quilt.loader

    mappings loom.layered() {
        // variantOf(libs.quilt.mappings) { classifier 'intermediary-v2' }
        officialMojangMappings()
		parchment("org.parchmentmc.data:parchment-1.21:2024.07.28@zip")
    }

	modImplementation include("org.jline:jline:3.26.1")
	modImplementation include("org.jline:jline-terminal-jansi:3.26.1")
    modImplementation "org.quiltmc:qsl:10.0.0-alpha.3+1.21.1"

	include modApi("com.moandjiezana.toml:toml4j:0.7.2") {
		exclude(module: "gson")
	}
}

processResources {
	inputs.properties 'version': version, 'group': project.group

	filesMatching('quilt.mod.json') {
		expand 'version': version, 'group': project.group
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'
	it.options.release = 21
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21

	withSourcesJar()
}

jar {
	from('LICENSE.md') {
		rename { "${it}_${base.archivesName.get()}" }
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
	}
}
